FIND_PROGRAM (GLSLANG_VALIDATOR_EXE
	NAMES glslangValidator
	HINTS ${Vulkan_INCLUDE_DIRS} ${Vulkan_LIBRARY_DIRS}
)

MESSAGE (STATUS "glslangValidator found at ${GLSLANG_VALIDATOR_EXE}")

SET (GLSL_INCLUDE_DIR ${CMAKE_CURRENT_SOURCE_DIR})

FUNCTION (COMPILE_SHADER TARGET_NAME GLSL_SRC_FILE)

	GET_FILENAME_COMPONENT (GLSL_SRC_FILE_NAME ${GLSL_SRC_FILE} NAME_WE)
	GET_FILENAME_COMPONENT (GLSL_SRC_FILE_EXT ${GLSL_SRC_FILE} EXT)

	STRING (SUBSTRING ${GLSL_SRC_FILE_EXT} 1 -1 GLSL_SRC_FILE_EXT_NO_DOT)

	GET_FILENAME_COMPONENT (GLSL_PARENT_PATH_NAME ${GLSL_SRC_FILE} PATH)
	GET_FILENAME_COMPONENT (GLSL_PARENT_NAME_NAME ${GLSL_PARENT_PATH_NAME} NAME)

	SET (SPV_FILE_NAME "${ASSET_DIR}/shader/${GLSL_PARENT_NAME_NAME}_${GLSL_SRC_FILE_NAME}_${GLSL_SRC_FILE_EXT_NO_DOT}.spv")

	ADD_CUSTOM_COMMAND (
		OUTPUT ${SPV_FILE_NAME}
		COMMAND ${GLSLANG_VALIDATOR_EXE}
			-I${GLSL_INCLUDE_DIR}
			-V ${GLSL_SRC_FILE}
			-o ${SPV_FILE_NAME}
		DEPENDS ${GLSL_SRC_FILE}
	)

	ADD_CUSTOM_TARGET (${TARGET_NAME} ALL DEPENDS ${SPV_FILE_NAME})

ENDFUNCTION ()

ADD_SUBDIRECTORY (debug)
ADD_SUBDIRECTORY (principled)
ADD_SUBDIRECTORY (environment)