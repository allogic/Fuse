FILE (GLOB C_SRC_FILES "*.c")
FILE (GLOB JAVA_SRC_FILES "*.java")

TARGET_SOURCES (engine PRIVATE ${C_SRC_FILES})

SET (JAVA_CLASS_DIR "${CMAKE_BINARY_DIR}/java_classes")
SET (JNI_HEADER_DIR "${CMAKE_CURRENT_SOURCE_DIR}")

FOREACH (JAVA_SRC_FILE ${JAVA_SRC_FILES})

	GET_FILENAME_COMPONENT (JAVA_FILE_NAME ${JAVA_SRC_FILE} NAME_WE)

	STRING (TOLOWER ${JAVA_FILE_NAME} JAVA_FILE_NAME_LOWER)
	STRING (SUBSTRING "${JAVA_FILE_NAME_LOWER}" 3 -1 JAVA_FILE_NAME_LOWER_STRIPPED)

	STRING (SUBSTRING "${JAVA_FILE_NAME_LOWER_STRIPPED}" 0 1 JAVA_FILE_NAME_FIRST_CHAR)
	STRING (TOUPPER "${JAVA_FILE_NAME_FIRST_CHAR}" JAVA_FILE_NAME_FIRST_CHAR_UPPER)
	STRING (SUBSTRING "${JAVA_FILE_NAME_LOWER_STRIPPED}" 1 -1 JAVA_FILE_NAME_REST)

	SET (OLD_C_HEADER_FILE "${CMAKE_CURRENT_SOURCE_DIR}/${JAVA_FILE_NAME_FIRST_CHAR_UPPER}${JAVA_FILE_NAME_REST}.h")
	SET (NEW_C_HEADER_FILE "${CMAKE_CURRENT_SOURCE_DIR}/eg_${JAVA_FILE_NAME_LOWER_STRIPPED}.h")

	ADD_CUSTOM_COMMAND (
		OUTPUT ${NEW_C_HEADER_FILE}
		COMMAND ${CMAKE_COMMAND} -E make_directory ${JAVA_CLASS_DIR}
		COMMAND javac -d ${JAVA_CLASS_DIR} -h ${JNI_HEADER_DIR} ${JAVA_SRC_FILE}
		COMMAND ${CMAKE_COMMAND} -E rename ${OLD_C_HEADER_FILE} ${NEW_C_HEADER_FILE}
		COMMENT "Compiling java sources and generating JNI headers"
		VERBATIM
	)

	LIST (APPEND C_HEADER_OUTPUTS ${NEW_C_HEADER_FILE})

ENDFOREACH ()

ADD_CUSTOM_TARGET (generate_c_binding_header ALL DEPENDS ${C_HEADER_OUTPUTS})